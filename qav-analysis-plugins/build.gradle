plugins {
    // provides configurations for annotation processing, and passes the annotation processor to
    // the javac "-processorpath" option:
    id "net.ltgt.apt" version "0.19"
}

apply plugin: 'groovy'

compileGroovy {
    groovyOptions.javaAnnotationProcessing = true
    aptOptions.processorArgs = [genDocDir: project(':qav-doc').projectDir.absolutePath + "/src-gen/generated-docs"]
}

dependencies {
    compile project(':qav-analysis-dsl')
    compile project(':qav-architecture')
    compile project(':qav-architecture-dsl')
    compile project(':qav-graph')
    compile project(':qav-graph-db')
    compile project(':qav-input-java')
    compile project(':qav-input')
    compile project(':util:qav-util')
    compile project(':qav-visualization')

    compileOnly project(':util:qav-doc-generator')         // qav-doc-generator contains the annotations; not required at runtime.
    annotationProcessor project(':util:qav-doc-generator') // qav-doc-generator contains the doc generator; not required at runtime.

    compile "org.codehaus.groovy:groovy-all:${project.ext.groovyVersion}"
    implementation group: 'com.google.guava', name: 'guava', version: guavaVersion

    // for Analysis Result I/O:
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: jacksonVersion
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: jacksonVersion
}

// since this project's build run creates these files using qav-doc-generator, we're responsible for cleanup as well
task deleteGeneratedDoc(type: Delete) {
    delete project(':qav-doc').projectDir.absolutePath + "/src-gen"
}

clean.dependsOn deleteGeneratedDoc

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    archiveClassifier.set 'javadoc'
    from groovydoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives groovydocJar
}
