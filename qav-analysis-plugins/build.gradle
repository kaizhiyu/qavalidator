
apply plugin: 'groovy'

compileGroovy {
    groovyOptions.javaAnnotationProcessing = true
    aptOptions.processorArgs = [genDocDir: project(':qav-doc').projectDir.absolutePath + "/src-gen/generated-docs"]
}

dependencies {
    compile project(':qav-analysis-dsl')
    compile project(':qav-architecture')
    compile project(':qav-architecture-dsl')
    compile project(':qav-graph')
    compile project(':qav-input-java')
    compile project(':qav-input-maven')
    compile project(':qav-input-traces')
    compile project(':qav-input-typescript')
    compile project(':util:qav-util')
    compile project(':qav-visualization')

    compileOnly project(':util:qav-doc-generator')         // qav-doc-generator contains the annotations; not required at runtime.
    annotationProcessor project(':util:qav-doc-generator') // qav-doc-generator contains the doc generator; not required at runtime.

    compile "org.slf4j:slf4j-api:${project.ext.slf4jVersion}"
    compile "ch.qos.logback:logback-core:${project.ext.logbackVersion}"
    compile "ch.qos.logback:logback-classic:${project.ext.logbackVersion}"

    compile "org.codehaus.groovy:groovy-all:${project.ext.groovyVersion}"
}

// since this project's build run creates these files using qav-doc-generator, we're responsible for cleanup as well
task deleteGeneratedDoc(type: Delete) {
    delete project(':qav-doc').projectDir.absolutePath + "/src-gen"
}

clean.dependsOn deleteGeneratedDoc

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from groovydoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives groovydocJar
}

publishing {
    publications {
        qavAnalysisPluginsPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact groovydocJar

            pom.withXml {
                asNode().appendNode('name', "QAvalidator :: ${project.name}")
                asNode().appendNode('description', project.description)
                asNode().appendNode('url', project.websiteUrl)

                asNode().children().last() + project.pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUsername') ? project.bintrayUsername : 'unknown'
    key = project.hasProperty('bintrayApiKey') ? project.bintrayApiKey : 'unknown'
    publications = ['qavAnalysisPluginsPublication']
    dryRun = false
    publish = true
    pkg {
        repo = project.bintrayRepo
        name = project.name
        desc = project.description
        licenses = ['Apache-2.0']
        labels = ['qaware', 'architecture', 'validation']
        websiteUrl = project.websiteUrl
        issueTrackerUrl = project.issueTrackerUrl
        vcsUrl = project.scmUrl
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = project.description
            released = new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZZ")
            vcsTag = "v${project.version}"
            mavenCentralSync {
                sync = false
            }
        }
    }
}